grammar Express {
	options {
		Axiom = "root";
		Separator = "SEPARATOR";
	}
	terminals {
		NEW_LINE -> U+000D /* CR */
					|  U+000A /* LF */
					|  U+000D U+000A /* CR LF */
					|  U+0085 // Next line character
					|  U+2028 // Line separator character
					|  U+2029; //Paragraph separator character (U+2029)
		WHITE_SPACE -> U+0020 | U+0009 | U+000B | U+000C;
		SEPARATOR   -> (WHITE_SPACE | NEW_LINE)+;
		IDENTIFIER	-> [a-zA-Z_][a-zA-Z0-9_]*;
		ANNOTATION	-> '(*' (.* - (.* '*)' .*)) '*)';
		INLINE_ANNOTATION		-> '--' (.* - (.* NEW_LINE .*)) NEW_LINE;
		INTEGER_LITERAL_DECIMAL -> ('0' | [1-9] [0-9]*) ([Uu] [Ll]? | [Ll] [Uu]? )? ;
		INTEGER_LITERAL_HEXA	-> '0' [xX] [a-fA-F0-9]+ ([Uu] [Ll]? | [Ll] [Uu]? )? ;
		REAL_LITERAL			-> ('0' | [0-9]+)? '.' ('0' | [0-9]+) ([eE] ('+'|'-')? ('0' | [1-9] [0-9]*))? [FfDdMm]?
								|  ('0' | [0-9]+) ([eE] ('+'|'-')? ('0' | [1-9] [0-9]*)) [FfDdMm]?
								|  ('0' | [0-9]+) [FfDdMm] ;
		BOOL_LITERAL	-> 'TRUE' | 'FALSE' | 'true' | 'false';
		STRING_LITERAL		-> '\'' (.* - (.* '\'' .*)) '\'';
		FUNCTION_BODY		-> 'FUNCTION' (.* - (.* 'END_FUNCTION' .*)) 'END_FUNCTION';
		CONSTANT_BODY		-> 'CONSTANT' (.* - (.* 'END_CONSTANT' .*)) 'END_CONSTANT';
		RULE_BODY	-> 'RULE' (.* - (.* 'END_RULE' .*)) 'END_RULE';
	}
	rules {
		root		-> comment_exp* schema_decl comment_exp*;
		comment_exp -> ANNOTATION;
		use_decl	-> 'USE'! 'FROM'! IDENTIFIER INLINE_ANNOTATION? ('('! IDENTIFIER (','! INLINE_ANNOTATION? IDENTIFIER)* ')'!)?';'!; //TODO: take into account parameters
		reference_decl	-> 'REFERENCE'! 'FROM'! IDENTIFIER INLINE_ANNOTATION? ('('! IDENTIFIER (','! INLINE_ANNOTATION? IDENTIFIER)* ')'!)?';'!; //TODO: process references
		constant_decl	-> CONSTANT_BODY ';'! ;
		schema_decl -> 'SCHEMA'! IDENTIFIER ';'! (comment_exp | use_decl | reference_decl | type_decl | entity_decl | function_decl | rule_decl | subtype_const_decl | constant_decl | INLINE_ANNOTATION)* 'END_SCHEMA' ';' INLINE_ANNOTATION?;
		type_decl	-> 'TYPE'! IDENTIFIER '='! 'EXTENSIBLE'? 'GENERIC_ENTITY'? (select_decl | enum_decl | type_id) based_on_exp? ';'! INLINE_ANNOTATION? where_decl? 'END_TYPE'! ';'!;
		based_on_exp	-> 'BASED_ON'! IDENTIFIER ('WITH'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!)? ; //TODO: process based expression
		select_decl	-> 'SELECT'! ('('! IDENTIFIER (','! IDENTIFIER)* ')'!)?;
		entity_decl	-> 'ENTITY'! IDENTIFIER 'ABSTRACT'? (subtype_decl | supertype_decl)* ';'! (prop_decl | comment_exp | INLINE_ANNOTATION)* derive_decl? unique_decl? inverse_decl? where_decl? 'END_ENTITY'! ';'!;
		enum_decl	-> 'ENUMERATION'! 'OF'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!;
		subtype_const_decl	-> 'SUBTYPE_CONSTRAINT'! IDENTIFIER 'FOR'! IDENTIFIER ';'! ('ABSTRACT'? 'SUPERTYPE'? ';'!)? (oneof_exp | andor_exp) ';'! 'END_SUBTYPE_CONSTRAINT'! ';'! ; //TODO: process subtype constraints
		//TODO: parse functions only if needed
		//function_decl	-> 'FUNCTION'! IDENTIFIER '('! (IDENTIFIER ':'! type_id (';'! IDENTIFIER ':'! type_id)*)? ')'! (':'! type_id)? ';'!
		//					statement_exp*
		//					'END_FUNCTION'! ';'!;
		function_decl	-> FUNCTION_BODY ';'!;
		statement_exp	-> local_decl | if_decl | return_statement;
		local_decl	-> 'LOCAL'! prop_decl* 'END_LOCAL'! ';';
		if_decl		-> 'IF'! bool_exp 'THEN'! statement_exp+ 'END_IF'! ';'!;
		return_statement	-> 'RETURN'! '('! (function_call_exp | literal) ')'! ';' ;
		rule_decl		-> RULE_BODY ';'! ;
		supertype_decl	-> 'SUBTYPE'! 'OF'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!;
		subtype_decl	-> 'SUPERTYPE'! ('OF'! '('! (oneof_exp | andor_exp | IDENTIFIER) ')'!)?;
		inverse_decl	-> 'INVERSE'! INLINE_ANNOTATION* inverse_exp+;
		inverse_exp		-> attr_read_exp ':'! type_id 'FOR'! type_id ';'! INLINE_ANNOTATION*;
		derive_decl	-> 'DERIVE'! INLINE_ANNOTATION* derive_exp+;
		derive_exp	-> attr_read_exp ':'! type_id ':='! (attr_read_exp | literal | sizeof_opd | function_call_exp) ';'! INLINE_ANNOTATION*;
		unique_decl     -> 'UNIQUE'! INLINE_ANNOTATION* unique_exp+;
		unique_exp      -> IDENTIFIER ':' attr_read_exp (',' attr_read_exp)* ';'! INLINE_ANNOTATION*;
		attr_read_exp   -> 'SELF' | IDENTIFIER | ('SELF' | function_call_exp | IDENTIFIER)('[' INTEGER_LITERAL_DECIMAL ']')?(('.' | '\\') IDENTIFIER ('[' INTEGER_LITERAL_DECIMAL ']')?)+;
		prop_decl	-> attr_read_exp renamed_exp? ':'! optional_decl? type_id ';'!; //TODO: process renamed construction
		renamed_exp	-> 'RENAMED'! IDENTIFIER;
		list_decl	-> 'LIST'! array_exp 'OF'! type_id;
		list_concat_exp	-> list_concat_opd ('*'! list_concat_opd)+ | list_ext;
		list_concat_opd	-> comp_opd | function_call_exp;
		list_ext    -> '['! comp_opd (','! comp_opd)* ']'!;
		set_decl		-> 'SET'! array_exp? 'OF'! type_id;
		array_decl		-> 'ARRAY'! array_exp? 'OF'! type_id;
		bag_decl		-> 'BAG'! array_exp? 'OF'! type_id;
		array_exp	-> '['! INTEGER_LITERAL_DECIMAL (':'! (INTEGER_LITERAL_DECIMAL | '?'))?']'!;
		optional_decl	-> 'OPTIONAL';
		where_decl	-> 'WHERE'! INLINE_ANNOTATION* rule_exp+;
		rule_exp	-> IDENTIFIER ':'! bool_exp ';'! INLINE_ANNOTATION*;
		bool_exp		-> xor_exp | bool_exp 'OR'! xor_exp;
		xor_exp		-> and_exp | xor_exp 'XOR'! and_exp; 
		and_exp		-> unary_exp | and_exp 'AND'! unary_exp;
		unary_exp	-> 'NOT'? atom_exp;
		atom_exp	-> exists_exp | comp_exp | limits_exp | function_call_exp | '('! bool_exp ')'!;
		exists_exp	-> 'EXISTS'! '('! attr_read_exp ')'!;
		query_exp   -> 'QUERY'! '('! bool_exp ('|'! bool_exp)* ')'!;
		limits_exp	-> '{'! numeric_lit lt_comp_op attr_read_exp lt_comp_op numeric_lit '}'!;
		comp_exp	-> comp_opd comp_op comp_opd;
		comp_opd	-> access_opd | typeof_opd | sizeof_opd | usedin_opd | classof_opd | query_exp | list_ext | '('! comp_opd ')'!;
		function_call_exp	-> IDENTIFIER '('! function_params_exp (','! function_params_exp)* ')'!;
		function_params_exp	-> access_opd | list_ext | list_append_exp | '?';
		typeof_opd	-> 'TYPEOF'! '('! access_opd ')'!;
		usedin_opd	-> 'USEDIN'! '('! access_opd (','! access_opd)* ')'!;
		access_opd	-> literal | str_concat_exp | attr_read_exp ;
		sizeof_opd  -> 'SIZEOF'! '('! (list_concat_exp | attr_read_exp | query_exp | exists_exp | usedin_opd) ')'!;
		classof_opd -> 'CLASSOF'! '('! type_id ')'!;
		comp_op		-> eq_op | num_op | in_op; //all comparison symbol
		eq_op		-> '=' | ':<>:' | ':=:' | '<>';
		in_op		-> 'IN';
		num_op		-> lt_comp_op | gt_comp_op; //Numerical comparison symbols
		lt_comp_op	-> '<' | '<=' | '<*'; //liter or equal comparison
		gt_comp_op	-> '>=' | '>' | '>*'; //greater or equal comparison
		oneof_exp	-> 'ONEOF' '('! IDENTIFIER (','! IDENTIFIER)+ ')'!;
		andor_exp	-> oneof_exp ('ANDOR'! oneof_exp)+;
		str_concat_exp	-> STRING_LITERAL ('+'! STRING_LITERAL)+;
		list_append_exp	-> IDENTIFIER '+'! list_ext;
		type_id		-> IDENTIFIER | pt_keyword | list_decl | set_decl | array_decl | bag_decl;
		pt_keyword	-> 'STRING' | 'NUMBER' | 'BOOLEAN' | 'LOGICAL' | 'BINARY' | 'INTEGER' | 'REAL'; //Primitive type keywords
		literal		-> numeric_lit | BOOL_LITERAL | STRING_LITERAL;
		numeric_lit	-> INTEGER_LITERAL_DECIMAL | INTEGER_LITERAL_HEXA | REAL_LITERAL;
	}
}