grammar Express {
	options {
		Axiom = "root";
		Separator = "SEPARATOR";
	}
	terminals {
		NEW_LINE -> U+000D /* CR */
					|  U+000A /* LF */
					|  U+000D U+000A /* CR LF */
					|  U+0085 // Next line character
					|  U+2028 // Line separator character
					|  U+2029; //Paragraph separator character (U+2029)
		WHITE_SPACE -> U+0020 | U+0009 | U+000B | U+000C;
		SEPARATOR   -> (WHITE_SPACE | NEW_LINE)+;
		IDENTIFIER	-> [a-zA-Z_][a-zA-Z0-9_]*;
		ANNOTATION	-> '(*' (.* - (.* '*)' .*)) '*)';
		INLINE_ANNOTATION		-> '--' (.* - (.* NEW_LINE .*)) NEW_LINE;
		INTEGER_LITERAL_DECIMAL -> ('0' | [1-9] [0-9]*) ([Uu] [Ll]? | [Ll] [Uu]? )? ;
		INTEGER_LITERAL_HEXA	-> '0' [xX] [a-fA-F0-9]+ ([Uu] [Ll]? | [Ll] [Uu]? )? ;
		REAL_LITERAL			-> ('0' | [1-9] [0-9]*)? '.' ('0' | [1-9] [0-9]*) ([eE] ('+'|'-')? ('0' | [1-9] [0-9]*))? [FfDdMm]?
								|  ('0' | [1-9] [0-9]*) ([eE] ('+'|'-')? ('0' | [1-9] [0-9]*)) [FfDdMm]?
								|  ('0' | [1-9] [0-9]*) [FfDdMm] ;
		STRING_LITERAL		-> '\'' (.* - (.* '\'' .*)) '\'';
	}
	rules {
		root		-> comment_exp* schema_body;
		comment_exp -> ANNOTATION;
		schema_body -> schema_decl (comment_exp | use_decl | type_decl | entity_decl)*;
		use_decl	-> 'USE'! 'FROM'! IDENTIFIER ';'!;
		schema_decl -> 'SCHEMA'! IDENTIFIER ';'!;
		type_decl	-> 'TYPE'! IDENTIFIER '='! (select_decl | enum_decl | type_id ) ';'! where_decl? 'END_TYPE'! ';'!;
		select_decl	-> 'SELECT'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!;
		entity_decl	-> 'ENTITY'! IDENTIFIER abstract_decl? (subtype_decl | supertype_decl)* ';'! (prop_decl | INLINE_ANNOTATION)* derive_decl? unique_decl? where_decl? 'END_ENTITY'! ';'!;
		enum_decl	-> 'ENUMERATION'! 'OF'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!;
		abstract_decl	-> 'ABSTRACT';
		supertype_decl	-> 'SUBTYPE'! 'OF'! '('! IDENTIFIER (','! IDENTIFIER)* ')'!;
		subtype_decl	-> 'SUPERTYPE'! 'OF'! '('! (oneof_exp | andor_exp) ')'!;
		derive_decl	-> 'DERIVE'! INLINE_ANNOTATION* derive_exp+;
		derive_exp	-> attr_read_exp ':'! type_id ':='! (attr_read_exp | literal) ';'! INLINE_ANNOTATION*;
		unique_decl     -> 'UNIQUE'! INLINE_ANNOTATION* unique_exp+;
		unique_exp      -> IDENTIFIER ':' attr_read_exp (',' attr_read_exp)* ';'! INLINE_ANNOTATION*;
		attr_read_exp	-> ('SELF' | 'SELF\\'IDENTIFIER | IDENTIFIER('\\'IDENTIFIER)?)('.'IDENTIFIER)*('['! INTEGER_LITERAL_DECIMAL ']'!)?;
		prop_decl	-> attr_read_exp ':'! optional_decl? type_id ';'!;
		list_decl	-> 'LIST'! array_exp 'OF'! type_id;
		list_concat_exp	-> (list_ext | comp_opd) ('*'! (list_ext | comp_opd))+ | list_ext;
		list_ext    -> '['! comp_opd (','! comp_opd)* ']'!;
		set_decl		-> 'SET'! array_exp 'OF'! type_id;
		array_decl		-> 'ARRAY'! array_exp 'OF'! type_id;
		bag_decl		-> 'BAG'! array_exp 'OF'! type_id;
		query_exp   -> 'QUERY'! '('! or_exp '|'! or_exp ')'!;
		array_exp	-> '['! INTEGER_LITERAL_DECIMAL (':'! (INTEGER_LITERAL_DECIMAL | '?'))?']'!;
		optional_decl	-> 'OPTIONAL';
		where_decl	-> 'WHERE'! INLINE_ANNOTATION* rule_exp+;
		rule_exp	-> IDENTIFIER ':'! or_exp ';'! INLINE_ANNOTATION*;
		or_exp		-> and_exp | or_exp 'OR'! and_exp;
		and_exp		-> unary_exp | and_exp 'AND'! unary_exp;
		unary_exp	-> 'NOT'? atom_exp;
		atom_exp	-> exists_exp | comp_exp | limits_exp | function_call_exp | '('! or_exp ')'!;
		exists_exp	-> 'EXISTS'! '('! IDENTIFIER ')'!;
		limits_exp	-> '{'! numeric_lit lt_comp_op 'SELF' lt_comp_op numeric_lit '}'!;
		comp_exp	-> comp_opd comp_op comp_opd;
		comp_opd	-> access_opd | typeof_opd | sizeof_opd | usedin_opd | '('! comp_opd ')'!;
		function_call_exp	-> IDENTIFIER '(' function_params_exp (','! function_params_exp)* ')';
		function_params_exp	-> access_opd | list_ext;
		typeof_opd	-> 'TYPEOF'! '('! access_opd ')'!;
		usedin_opd	-> 'USEDIN'! '('! access_opd (','! access_opd)* ')'!;
		access_opd	-> literal | str_concat_exp | attr_read_exp ;
		sizeof_opd  -> 'SIZEOF'! '('! (list_concat_exp | attr_read_exp | query_exp) ')'!;
		comp_op		-> eq_op | num_op | in_op; //all comparison symbol
		eq_op		-> '=' | ':<>:' | ':=:' | '<>';
		in_op		-> 'IN';
		num_op		-> lt_comp_op | gt_comp_op; //Numerical comparison symbols
		lt_comp_op	-> '<' | '<=' | '<*'; //liter or equal comparison
		gt_comp_op	-> '>=' | '>' | '>*'; //greater or equal comparison
		oneof_exp	-> 'ONEOF' '('! IDENTIFIER (','! IDENTIFIER)+ ')'!;
		andor_exp	-> '('! oneof_exp ')'! ('ANDOR'! '('! oneof_exp ')'!)+;
		str_concat_exp	-> STRING_LITERAL ('+'! STRING_LITERAL)+;
		type_id		-> IDENTIFIER | pt_keyword | list_decl | set_decl | array_decl | bag_decl;
		pt_keyword	-> 'STRING' | 'NUMBER' | 'BOOLEAN' | 'LOGICAL' | 'BINARY' | 'INTEGER' | 'REAL'; //Primitive type keywords
		literal		-> 'true' | 'false' | numeric_lit | STRING_LITERAL;
		numeric_lit	-> INTEGER_LITERAL_DECIMAL | INTEGER_LITERAL_HEXA | REAL_LITERAL;
	}
}